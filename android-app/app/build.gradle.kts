import java.util.Properties

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("org.jetbrains.kotlin.plugin.compose")
}

android {
    namespace = "com.scooterre.client"
    compileSdk = 35

    defaultConfig {
        applicationId = "com.scooterre.client"
        minSdk = 26 // AES/CCM via the platform provider needs API 26+ (Conscrypt)
        targetSdk = 35
        versionCode = 35
        versionName = "4.1"
    }

    // Release signing key lives outside the repo: ~/.scooter-signing/keystore.properties
    // (storeFile, storePassword, keyAlias, keyPassword). Without it a release build stays unsigned;
    // debug builds are unaffected.
    val signingProps = Properties().apply {
        val f = File(System.getProperty("user.home"), ".scooter-signing/keystore.properties")
        if (f.exists()) f.inputStream().use { load(it) }
    }
    val hasReleaseKey = signingProps.getProperty("storeFile")?.let { File(it).exists() } == true

    signingConfigs {
        if (hasReleaseKey) {
            create("release") {
                storeFile = File(signingProps.getProperty("storeFile"))
                storePassword = signingProps.getProperty("storePassword")
                keyAlias = signingProps.getProperty("keyAlias")
                keyPassword = signingProps.getProperty("keyPassword")
            }
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            if (hasReleaseKey) signingConfig = signingConfigs.getByName("release")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        compose = true
    }
}

dependencies {
    implementation(project(":core"))
    implementation("androidx.core:core-ktx:1.13.1")
    implementation("androidx.appcompat:appcompat:1.7.0")
    implementation("androidx.activity:activity-ktx:1.9.2")
    implementation("androidx.activity:activity-compose:1.9.2")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.8.6")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.8.6")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1")

    implementation(platform("androidx.compose:compose-bom:2024.09.03"))
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-graphics")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.compose.material3:material3")

    implementation("androidx.security:security-crypto:1.1.0-alpha06")
    implementation("androidx.biometric:biometric:1.1.0")
    implementation("androidx.work:work-runtime-ktx:2.9.1")
    testImplementation("junit:junit:4.13.2")
    testImplementation("org.json:json:20231013")
    implementation("com.squareup.okhttp3:okhttp:4.12.0")
    implementation("androidx.browser:browser:1.8.0")
    // Home-screen widget - Glance instead of classic RemoteViews/XML since it lets the widget UI
    // be written in the same Compose-like style as the rest of the app.
    implementation("androidx.glance:glance-appwidget:1.1.1")
    // Data Layer sync to the optional Wear OS companion (:wear) - status relay and settings/key push.
    implementation("com.google.android.gms:play-services-wearable:20.0.1")
}
